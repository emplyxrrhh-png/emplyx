; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Visualtime Secure Connect"
#define MyAppVersion "1.0"
#define MyAppPublisher "cegid"
#define MyAppURL "https://www.visualtime.net"
#define MyAppExeName "VisualtimeSecureConnect.exe"
#define MyAPIEndPoint "https://apiservice.visualtime.net/api/sc/"
#define MyUpdaterExeName "Visualtime SC Updater.exe"

[Dirs]
Name: "{app}\runtimes\win\lib\net7.0-windows"

[Languages]
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Setup]
AppId={{A2CA9AF9-06B1-4468-844A-050E42BED8D2}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\cegid\{#MyAppName}
ChangesAssociations=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=Visualtime Secure Connect Setup     
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Setup]  
PrivilegesRequired=admin

[Messages]
SetupAppTitle=Instalación de Visualtime Secure Connect
       
[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\bin\Release\net7.0-windows\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\endpointsConfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\runtimes\win\lib\net7.0\System.Diagnostics.EventLog.dll"; DestDir: "{app}\runtimes\win\lib\net7.0"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\runtimes\win\lib\net7.0\System.Diagnostics.EventLog.Messages.dll"; DestDir: "{app}\runtimes\win\lib\net7.0"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\runtimes\win\lib\net7.0\System.ServiceProcess.ServiceController.dll"; DestDir: "{app}\runtimes\win\lib\net7.0"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\VisualtimeSecureConnect.deps.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\VisualtimeSecureConnect.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\bin\Release\net7.0-windows\VisualtimeSecureConnect.runtimeconfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\Visualtime SC Updater\bin\Release\net7.0-windows\Visualtime SC Updater.runtimeconfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\Visualtime SC Updater\bin\Release\net7.0-windows\Visualtime SC Updater.deps.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\Visualtime SC Updater\bin\Release\net7.0-windows\{#MyUpdaterExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\Visualtime SC Updater\bin\Release\net7.0-windows\Visualtime SC Updater.dll"; DestDir: "{app}"; Flags: ignoreversion
             
[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: {sys}\sc.exe; Parameters: "create ""Visualtime Secure Connect"" start= auto binPath= ""{app}\{#MyAppExeName}""" ; Flags: runhidden;
Filename: {sys}\sc.exe; Parameters: "description ""Visualtime Secure Connect"" ""Servidor de conexiones seguras para terminales Visualtime"""; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "failure ""Visualtime Secure Connect"" reset= 86400 actions= restart/60000/restart/60000/restart/60000"; Flags: runhidden
Filename: {sys}\netsh.exe; Parameters: "advfirewall firewall add rule name=""Visualtime Secure Connect"" dir=in program=""{app}\{#MyAppExeName}"" security=notrequired action=allow"; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "create ""Visualtime SC Updater"" start= auto binPath= ""{app}\{#MyUpdaterExeName}""" ; Flags: runhidden;
Filename: {sys}\sc.exe; Parameters: "description ""Visualtime SC Updater"" ""Actualizador automático del servicio Visualtime Secure Connect"""; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "failure ""Visualtime SC Updater"" reset= 86400 actions= restart/60000/restart/60000/restart/60000"; Flags: runhidden
 
[UninstallRun]
Filename: {sys}\sc.exe; Parameters: "stop ""Visualtime Secure Connect""" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "delete ""Visualtime Secure Connect""" ; Flags: runhidden
Filename: {sys}\netsh.exe; Parameters: "advfirewall firewall delete rule name=all program=""{app}\{#MyAppExeName}"""; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "stop ""Visualtime SC Updater""" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "delete ""Visualtime SC Updater""" ; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}*"

[Code]
function HTTPRequest(URL, RequestMethod, RequestData: string; var ResponseText: WideString; var ApiAvailable: Boolean): Boolean;
var
  WinHttpReq: Variant;
begin
  try
    WinHttpReq := CreateOleObject('WinHttp.WinHttpRequest.5.1');
    WinHttpReq.Open(RequestMethod, URL, False);
    WinHttpReq.SetRequestHeader('Content-Type', 'application/json'); 
    ApiAvailable := true;
    try
        WinHttpReq.Send(RequestData);
    except
       ApiAvailable:= false;
    end;
    
    if WinHttpReq.Status = 200 then
    begin
      ResponseText := WinHttpReq.ResponseText;
      Result := True;
    end
    else
    begin
      // Manejo de errores o códigos de respuesta no exitosos
      ResponseText := 'Error: ' + IntToStr(WinHttpReq.Status);
      Result := False;
    end;
  except
    Result := False;
  end;
end;

var
  WelcomePage: TWizardPage;
  CredentialsPage: TInputQueryWizardPage;
  DiagnosticsPage: TInputQueryWizardPage;
  IdCliente: string;
  Token: string;
  fileName : string;
  path: string;
  optionsContent: string;
  CustomValue: string;
  WelcomeLabel: TNewStaticText;
  URL: string;
  RequestMethod: string;
  RequestData: WideString;
  ResponseText: WideString;
  ApiAvailable: Boolean;
procedure InitializeWizard;
begin
  DiagnosticsPage := CreateInputQueryPage(wpWelcome, 'Diagnósticos', 'Indique, si lo desea, el puerto de la página de diagnósticos de Visualtime Secure Connect', 
  'Si indica un puerto, podrá acceder mediante http://ip:puerto a una página donde se mostrará el estado del servicio Visualtime Secure Connect.' + #13#10 + 'Si no desea disponer de esa página de diagnósticos, deje el dato en blanco.');
  DiagnosticsPage.Add('Puerto para diagnósticos (dejar en blanco para deshabilitar):', False);
  DiagnosticsPage.Values[0] := '8088';
  CredentialsPage := CreateInputQueryPage(wpWelcome, 'Credenciales', 'Indique a continuación sus credenciales de acceso a la API de terminales de Cegid', 
  'Para consultar su identificador de cliente y generar su token de acceso a la API de terminales, acceda a Visualtime (Configuracón / Visualtime / Administración WS).' + #13#10 + #13#10 + 'Si no dispone de estos datos, o no está seguro, cancele la instalación ahora e inténtelo más tarde.' + #13#10 + #13#10) ;
  CredentialsPage.Add('Identificador de cliente:', False);
  CredentialsPage.Add('Token de autorización:', False);
  CredentialsPage.Add('URL de la API:', False);
  CredentialsPage.Values[2] := '{#MyAPIEndPoint}';
  WelcomePage := CreateCustomPage(wpWelcome, 'Bienvenido a la Instalación', 
                '¡Bienvenido a la instalación de Visualtime Secure Connect!');
  // Crea una etiqueta para texto adicional
  WelcomeLabel := TNewStaticText.Create(WelcomePage);
  WelcomeLabel.Parent := WelcomePage.Surface;
  WelcomeLabel.Left := ScaleX(0);
  WelcomeLabel.Top := ScaleY(40);
  WelcomeLabel.Width := WelcomePage.SurfaceWidth - ScaleX(32);
  WelcomeLabel.Height := ScaleY(60);
  WelcomeLabel.Caption :=
                'Este instalador desplegará el servicio Visualtime Secure Connect en esta máquina.' + #13#10 + #13#10 +
                'Cuando acabe la instalación, podrá redirigir los terminales compatibles de los ' + #13#10 + 
                'que disponga (modelos mx8, rxCeP, rxCP o mxC) a la IP o DNS de esta máquina, y por ' + #13#10 + 
                'el puerto correspondiente según el modelo.' + #13#10 +       #13#10 +
                'Hecho esto, sus terminales conectarán con el servidor de comunicaciones de Visualtime ' + #13#10 + 
                'a través de un túnel SSL.' + #13#10 + #13#10 + 
                'La redirección de terminales se puede hacer de forma progresiva. Mientras no haga la ' + #13#10 +
                'redirección, sus terminales seguirán operativos.' + #13#10 + #13#10 + 
                'Para el correcto funcionamiento del servicio, esta máquina debe tener instalado el  ' +  #13#10 +
                'SDK de .NET 7.0. Si no está seguro de si está instalado, consulte con su departamento ' +  #13#10 +
                'técnico antes de iniciar la instalación'  
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    fileName := ExpandConstant('{app}\proxyConfig.json');
              optionsContent := 
                '{' + #13#10 +
                '  "version": "' + '{#MyAppVersion}' + '",' + #13#10 +
                '  "role": "Terminal",' + #13#10 +
                '  "apiUrl": "' + CredentialsPage.Values[2] + '",' + #13#10 +
                '  "apiToken": "' + CredentialsPage.Values[1] + '",' + #13#10 +
                '  "customerId": "' + CredentialsPage.Values[0] + '",' + #13#10 +
                '  "emergency": "true",' + #13#10 +
                '  "diagnosticsPort": "' + DiagnosticsPage.Values[0] + '"' + #13#10 +
                '}';
              SaveStringToFile(fileName, optionsContent, True);    
  end;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = CredentialsPage.ID then
  begin
    IdCliente := CredentialsPage.Values[0]; 
    Token := CredentialsPage.Values[1]; 

    if Trim(IdCliente) = '' then
    begin
      MsgBox('Debes indicar el identificador de cliente.', mbError, MB_OK);
      Result := False; 
    end
    else
    begin
      if Trim(Token) = '' then
      begin
        MsgBox('Debes indicar el token del cliente.', mbError, MB_OK);
        Result := False; 
      end
      else
        begin
          URL := CredentialsPage.Values[2] + 'endpoint/' + CredentialsPage.Values[0] + '/' + CredentialsPage.Values[1] + '/'; 
          RequestMethod := 'GET'; 
          RequestData := ''; 

          if HTTPRequest(URL, RequestMethod, RequestData, ResponseText, ApiAvailable) then
            begin 
              Result := True;
            end
          else
            begin
               if ApiAvailable then
               begin
               MsgBox('Las credenciales no son válidas. Recuerde que puede consultarlas en Visualtime (Configuracón / Visualtime / Administración WS)', mbError, MB_OK);
               end
               else
               begin
               MsgBox('La URL no es válida o no es accesible desde este equipo', mbError, MB_OK);
               end 
            end
        end
    end;
  end
  else
  begin
    Result := True;
  end;
end;



